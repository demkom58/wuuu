buildscript {
    dependencies {
        classpath('com.google.cloud.tools:jib-spring-boot-extension-gradle:0.1.0')
    }
}

plugins {
    id 'org.springframework.boot' version '3.2.4' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
    id 'com.netflix.dgs.codegen' version '6.0.3' apply false
    id 'org.graalvm.buildtools.native' version '0.9.28' apply false
    id 'com.google.cloud.tools.jib' version '3.4.1' apply false
}

group = 'io.wuuu'
version = '1.0-SNAPSHOT'

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://repo.spring.io/snapshot' }
    }

    pluginManager.withPlugin('java') {
        java.sourceCompatibility = '21'
        java.targetCompatibility = '21'
        java.toolchain.languageVersion = JavaLanguageVersion.of(21)
        compileJava.options.encoding = 'UTF-8'

        configurations {
            compileOnly {
                extendsFrom annotationProcessor
            }
        }
    }

    pluginManager.withPlugin('com.google.cloud.tools.jib') {
        jib {
            from {
                image = 'demkom58/curl-openjdk:21-slim'
            }
            to {
                tags = ['latest', project.version.toString()]
                image = 'wuuu/' + project.projectDir.name
            }
            pluginExtensions {
                pluginExtension {
                    implementation = 'com.google.cloud.tools.jib.gradle.extension.springboot.JibSpringBootExtension'
                }
            }
        }
    }
}
